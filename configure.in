dnl Process this file with autoconf to produce a configure script.

dnl ---------------------------------------------------------------------------
dnl Initialize automake now
dnl ---------------------------------------------------------------------------

AC_INIT()

AC_CANONICAL_HOST

XFCE_PACKAGE=xfce

LIB_MAJOR=1
LIB_MINOR=0
LIB_TINY=0

LIBVERSION=${LIB_MAJOR}:${LIB_MINOR}:${LIB_TINY}

XFCE_VERSION=3.6.3

AC_SUBST(VERSION, $XFCE_VERSION)
AC_SUBST(LIBVERSION)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE($XFCE_PACKAGE, $XFCE_VERSION)
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_DISABLE_STATIC
AM_PROG_LIBTOOL

ALL_LINGUAS="ca cs da de es et fi fr gl hu it ja nl pl pt_BR ro ru sk sv zh"
AM_GNU_GETTEXT
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_CHECK_HEADERS(ctype.h)
AC_CHECK_HEADERS(unistd.h string.h getopt.h)
AC_CHECK_HEADERS(fcntl.h)
AC_HEADER_STAT
AC_HEADER_DIRENT
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME
AC_PATH_X
AC_PATH_XTRA

AC_TYPE_SIGNAL

AC_CHECK_FUNCS(wait3 wait4 waitpid)
AC_CHECK_FUNCS(snprintf vsnprintf strcasecmp strncasecmp)
AC_CHECK_FUNCS(sigaction siginterrupt)

AM_PATH_GTK(1.2.3, ,
            AC_MSG_ERROR(Cannot find GTK: Is gtk-config in path?))

dnl test for Xpm library
_cppflags="${CPPFLAGS}"
_ldflags="${LDFLAGS}"

AC_ARG_WITH(xpm,
[  --with-xpm=PFX          path to Xpm library],[
  if test "$withval" != "no"; then
    CPPFLAGS="${CPPFLAGS} -I$withval/include"
    LDFLAGS="${LDFLAGS} -L$withval/lib"
  fi
])

AC_ARG_ENABLE(gtk_engine,
[  --enable-gtk-engine=PFX path to GTK engine libraries],[
  GTK_ENGINE_DIR=$enableval
],[
  GTK_ENGINE_DIR=`$GTK_CONFIG --prefix`/lib/gtk/themes/engines
])
AC_SUBST(GTK_ENGINE_DIR)

AC_CHECK_HEADER(X11/xpm.h,
  AC_CHECK_LIB(Xpm, XpmReadFileToXpmImage, ,
    AC_MSG_WARN(*** X11/xpm.h found but Xpm library not found)
    CPPFLAGS="${_cppflags}" LDFLAGS="${_ldflags}", -lX11 $X_LIBS),
  AC_MSG_WARN(*** X11/xpm.h not found)
  CPPFLAGS="${_cppflags}" LDFLAGS="${_ldflags}")
 
dnl Check the availability of Session Management
AC_CHECK_LIB(SM, SmcOpenConnection, AC_DEFINE(HAVE_SESSION), ,
             [$X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS])

dnl test for imlib library
AC_ARG_ENABLE(imlib,
[  --enable-imlib=PFX      prefix where Imlib is installed (optional)],
  [
    if test "$enableval" != "no"; then
      if test -x "$enableval/bin/imlib-config"; then
        IMLIB_CONFIG=$enableval/bin/imlib-config
      else
        IMLIB_CONFIG=imlib-config
      fi

      GFX_LIB_IMLIB=`$IMLIB_CONFIG --libs`
      GFX_LIB_GDK_IMLIB=`$IMLIB_CONFIG --libs-gdk`
      IMLIB_CFLAGS=`$IMLIB_CONFIG --cflags`
      GDK_IMLIB_CFLAGS=`$IMLIB_CONFIG --cflags-gdk`

      AC_CHECK_LIB(Imlib, Imlib_init,
        IMLIB_LIBS=$GFX_LIB_IMLIB; AC_DEFINE(HAVE_IMLIB), ,
        $GFX_LIB_IMLIB)
      AC_CHECK_LIB(gdk_imlib, gdk_imlib_init,
        GDK_IMLIB_LIBS=$GFX_LIB_GDK_IMLIB; AC_DEFINE(HAVE_GDK_IMLIB), ,
        $GFX_LIB_GDK_IMLIB)
    fi
  ],
  [
    IMLIB_CONFIG=imlib-config

    GFX_LIB_IMLIB=`$IMLIB_CONFIG --libs`
    GFX_LIB_GDK_IMLIB=`$IMLIB_CONFIG --libs-gdk`
    IMLIB_CFLAGS=`$IMLIB_CONFIG --cflags`
    GDK_IMLIB_CFLAGS=`$IMLIB_CONFIG --cflags-gdk`

    AC_CHECK_LIB(Imlib, Imlib_init,
      IMLIB_LIBS=$GFX_LIB_IMLIB; AC_DEFINE(HAVE_IMLIB), ,
      $GFX_LIB_IMLIB)
    AC_CHECK_LIB(gdk_imlib, gdk_imlib_init,
      GDK_IMLIB_LIBS=$GFX_LIB_GDK_IMLIB; AC_DEFINE(HAVE_GDK_IMLIB), ,
      $GFX_LIB_GDK_IMLIB)
  ]
)

AC_SUBST(HAVE_IMLIB)
AC_SUBST(HAVE_GDK_IMLIB)
AC_SUBST(IMLIB_LIBS)
AC_SUBST(GDK_IMLIB_LIBS)
AC_SUBST(IMLIB_CFLAGS)
AC_SUBST(GDK_IMLIB_CFLAGS)

if test "x$GDK_IMLIB_LIBS" = "x"; then
  dnl test for gdk-pixbuf library
  AC_ARG_ENABLE(gdk-pixbuf,
  [  --enable-gdk-pixbuf=PFX prefix where gdk-pixbuf is installed (optional, only selected if imlib is missing)],
    [
      if test "$enableval" != "no"; then
        if test -x "$enableval/bin/gdk-pixbuf-config"; then
          GDK_PIXBUF_CONFIG=$enableval/bin/gdk-pixbuf-config
        else
          GDK_PIXBUF_CONFIG=gdk-pixbuf-config
        fi

        GFX_LIB_GDK_PIXBUF=`$GDK_PIXBUF_CONFIG --libs`
        GDK_PIXBUF_CFLAGS=`$GDK_PIXBUF_CONFIG --cflags`

        AC_CHECK_LIB(gdk_pixbuf, gdk_pixbuf_new,
          GDK_PIXBUF_LIBS=$GFX_LIB_GDK_PIXBUF; AC_DEFINE(HAVE_GDK_PIXBUF), ,
          $GFX_LIB_GDK_PIXBUF)
      fi
    ],
    [
      GDK_PIXBUF_CONFIG=gdk-pixbuf-config

      GFX_LIB_GDK_PIXBUF=`$GDK_PIXBUF_CONFIG --libs`
      GDK_PIXBUF_CFLAGS=`$GDK_PIXBUF_CONFIG --cflags`

      AC_CHECK_LIB(gdk_pixbuf, gdk_pixbuf_new,
          GDK_PIXBUF_LIBS=$GFX_LIB_GDK_PIXBUF; AC_DEFINE(HAVE_GDK_PIXBUF), ,
          $GFX_LIB_GDK_PIXBUF)
    ]
  )
fi

AC_SUBST(HAVE_GDK_PIXBUF)
AC_SUBST(GDK_PIXBUF_LIBS)
AC_SUBST(GDK_PIXBUF_CFLAGS)

AC_ARG_ENABLE(xinerama,
[  --enable-xinerama       enable xinerama extension [default]
  --disable-xinerama      Don't use xinerama extension even if available], [],
 [enable_xinerama=yes])

XINERAMA_LIBS=""
if test "x$enable_xinerama" = "xyes"; then
        AC_CHECK_LIB(Xinerama, XineramaQueryScreens,
	        [XINERAMA_LIBS="-lXinerama"
	         AC_CHECK_HEADERS(X11/extensions/Xinerama.h)],
	         [],[$X_LIBS -X11 -lXext])
fi
AC_SUBST(XINERAMA_LIBS)

dnl enable CDE-specific startup files?
AC_MSG_CHECKING(whether to install CDE specific files)
AC_ARG_ENABLE(dt,
        [  --enable-dt             enable CDE specific files],
        [USE_DT=$enableval],
        [USE_DT=no]
)
AC_MSG_RESULT($USE_DT)
AC_SUBST(USE_DT)

AC_ARG_WITH(dt-suffix,
[  --with-dt-suffix=STR    string to append to CDE filenames],[
  AC_MSG_CHECKING(for string to append to CDE filenames)
  DT_SUFFIX="$withval"
  AC_MSG_RESULT($DT_SUFFIX)
])
AC_SUBST(DT_SUFFIX)

compiler_warnings=yes
AC_MSG_CHECKING(if we want compiler warnings)
AC_ARG_ENABLE(
	warnings,
	[  --enable-warnings       [default=yes] enable gcc compiler warnings],
	compiler_warnings="$enableval", compiler_warnings="yes"
	)
AC_MSG_RESULT($compiler_warnings)

if test "$ac_cv_prog_CC" = gcc -o "$ac_cv_prog_CC" = g++; then
  if test $compiler_warnings = yes; then
    if echo "$CFLAGS" | grep "\-Wall" > /dev/null 2> /dev/null; then
      CFLAGS="$CFLAGS"
    else
      echo "updating CFLAGS with extra '-Wall' option"
      CFLAGS="$CFLAGS -Wall"
    fi

dnl ---------------------------------------------------------------------------
dnl depending on the version of gcc used, using '-Wno-implicit-int' oppresses
dnl the ridiculous messages printed because of the X11 prototypes from the
dnl X11 headers.  the downside to doing this, of course, is that code in
dnl the application itself which have implicit int declarations will go
dnl unnoticed.  but oh well; whomever writes the code should be sure not to
dnl use any implicit declarations.
dnl ---------------------------------------------------------------------------
    if echo "$CFLAGS" | grep "\-Wno-implicit-int" > /dev/null 2> /dev/null; then
      CFLAGS="$CFLAGS"
    else
      echo "updating CFLAGS with extra '-Wno-implicit-int' option"
      CFLAGS="$CFLAGS -Wno-implicit-int"
    fi
  fi
fi

dnl AC_ARG_ENABLE(xfce_theme,
dnl 	[  --enable-xfce-theme        [default=yes] build xfce_theme],
dnl         [ XFCE_THEME=$enableval,
dnl )
dnl AC_SUBST(XFCE_THEME)

AC_ARG_ENABLE(dmalloc,
	[  --enable-dmalloc=PFX    [default=no] enable dmalloc library],
        [ DMALLOC_LIB=$enableval/libdmalloc.a
          AC_DEFINE(DMALLOC)],
)
AC_SUBST(DMALLOC_LIB)

AC_ARG_ENABLE(old_style,
[  --enable-old-style      Enable old style for xfce panel [default=no]],[
  AC_DEFINE(OLD_STYLE)
],)

AC_MSG_CHECKING(if prototype for gethostname exists)
AC_EGREP_CPP(gethostname,[
#include <unistd.h>],
AC_MSG_RESULT(yes)
AC_DEFINE(HAVE_GETHOSTNAME_PROTO),
AC_MSG_RESULT(no))

dnl The following is to ensure $prefix is properly expanded
AC_MSG_CHECKING(XFce directory structure)
AC_ARG_WITH(data-dir,
[  --with-data-dir=DIR     Set XFce data directory to DIR],[
  XFCE_DIR=$withval],[
if test "x$datadir" = "x\${prefix}/share" -o "x$prefix" = "xNONE";
then
  XFCE_DIR="/usr/local/share/xfce"
else
  XFCE_DIR="$datadir/xfce"
fi])
AC_SUBST(XFCE_DIR)

AC_ARG_WITH(conf-dir,
[  --with-conf-dir=DIR     Set XFce configuration directory to DIR],[
  XFCE_CONFDIR=$withval],[
XFCE_CONFDIR=`echo $XFCE_CONFDIR | sed "s,\${prefix},${prefix},"`
if test "x$sysconfdir" = "x\${prefix}/etc" -o "x$prefix" = "xNONE";
then
  XFCE_CONFDIR="/usr/local/etc/xfce"
else
  XFCE_CONFDIR="$sysconfdir/xfce"
fi])
AC_SUBST(XFCE_CONFDIR)

AC_ARG_WITH(locale-dir,
[  --with-locale-dir=DIR   Set XFce locale directory to DIR],[
  XFCE_LOCALE_DIR=$withval],[
if test "x$prefix" = "x" -o "x$prefix" = "xNONE"; then
  XFCE_LOCALE_DIR="/usr/local/share/locale"
else
  XFCE_LOCALE_DIR="$prefix/share/locale"
fi])
AC_SUBST(XFCE_LOCALE_DIR)

AC_MSG_RESULT(ok)
AC_MSG_RESULT(XFCE_DIR set to $XFCE_DIR)
AC_MSG_RESULT(XFCE_LOCALE_DIR set to $XFCE_LOCALE_DIR)
AC_MSG_RESULT(XFCE_CONFDIR set to $XFCE_CONFDIR)

AC_OUTPUT(Makefile libs/Makefile xfce/Makefile xfwm/Makefile \
	xftree/Makefile xfpager/Makefile xfmouse/Makefile xfrun/Makefile \
	xfbd/Makefile xfclock/Makefile xfsound/Makefile xfgnome/Makefile \
	scripts/Makefile intl/Makefile po/Makefile.in \
	other/Makefile.in backdrops/Makefile.in icons/Makefile.in \
	palettes/Makefile.in sounds/Makefile.in dt/Makefile \
	html/Makefile.in textures/Makefile.in xfce_theme/Makefile)
dnl
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" scripts/xfce_setup.in > scripts/xfce_setup])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" scripts/xfce_upgrade.in > scripts/xfce_upgrade])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" scripts/xfhelp.in > scripts/xfhelp])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" scripts/startxfce.in > scripts/startxfce])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfbdrc.in >  other/xfbdrc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfsoundrc.in > other/xfsoundrc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.in >  other/xfce3rc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.ja.in >  other/xfce3rc.ja])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.cs.in >  other/xfce3rc.cs])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.hu.in >  other/xfce3rc.hu])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.ko.in >  other/xfce3rc.ko])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.zh_CN.GB2312.in >  other/xfce3rc.zh_CN.GB2312])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfce3rc.zh_TW.Big5.in >  other/xfce3rc.zh_TW.Big5])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" -e "s%XFCE_CONFDIR%$XFCE_CONFDIR%g" other/xfwmrc.in >  other/xfwmrc])
AC_OUTPUT_COMMANDS([sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r other/DISTFILES.in" -e "/CONFFILES =/r other/CONFFILES.in" other/Makefile.in > other/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r backdrops/DISTFILES.in" backdrops/Makefile.in > backdrops/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r textures/DISTFILES.in" textures/Makefile.in > textures/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r icons/DISTFILES.in" icons/Makefile.in > icons/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r palettes/DISTFILES.in" palettes/Makefile.in > palettes/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r sounds/DISTFILES.in" sounds/Makefile.in > sounds/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r html/DISTFILES.in" html/Makefile.in > html/Makefile])
