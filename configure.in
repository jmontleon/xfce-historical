dnl Process this file with autoconf to produce a configure script.

dnl ---------------------------------------------------------------------------
dnl Initialize automake now
dnl ---------------------------------------------------------------------------

AC_INIT()

AC_CANONICAL_HOST

XFCE_PACKAGE=xfce
XFCE_VERSION=3.2.5

AC_SUBST(PACKAGE, $XFCE_PACKAGE)
AC_SUBST(VERSION, $XFCE_VERSION)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE($XFCE_PACKAGE, $XFCE_VERSION)
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB

ALL_LINGUAS="ca da de es et fi fr gl it ja pl ru sk sv zh"
AM_GNU_GETTEXT
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

AC_CHECK_HEADERS(ctype.h)
AC_CHECK_HEADERS(unistd.h string.h getopt.h)
AC_CHECK_HEADERS(fcntl.h)
AC_HEADER_STAT
AC_HEADER_DIRENT
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME
AC_PATH_X
AC_PATH_XTRA

AC_TYPE_SIGNAL
AC_CHECK_FUNCS(snprintf vsnprintf strcasecmp strncasecmp)

AM_PATH_GTK(1.2.0, ,
            AC_MSG_ERROR(Cannot find GTK: Is gtk-config in path?))

dnl test for Xpm library
_cppflags="${CPPFLAGS}"
_ldflags="${LDFLAGS}"

AC_ARG_WITH(xpm,
[  --with-xpm=PFX          path to Xpm library],[
  if test "$withval" != "no"; then
    CPPFLAGS="${CPPFLAGS} -I$withval/include"
    LDFLAGS="${LDFLAGS} -L$withval/lib"
  fi
])

AC_CHECK_HEADER(X11/xpm.h,
  AC_CHECK_LIB(Xpm, XpmReadFileToXpmImage, ,
    AC_MSG_WARN(*** X11/xpm.h found but Xpm library not found)
    CPPFLAGS="${_cppflags}" LDFLAGS="${_ldflags}", -lX11 $X_LIBS),
  AC_MSG_WARN(*** X11/xpm.h not found)
  CPPFLAGS="${_cppflags}" LDFLAGS="${_ldflags}")
 
dnl test for imlib library
AC_ARG_ENABLE(imlib,
        [  --disable-imlib         disable use of imlib even if available],,
        [GFX_LIB_IMLIB=`imlib-config --libs`
	 GFX_LIB_GDK_IMLIB=`imlib-config --libs-gdk`
	 IMLIB_CFLAGS=`imlib-config --cflags`
	 GDK_IMLIB_CFLAGS=`imlib-config --cflags-gdk`
	 AC_CHECK_LIB(Imlib, 
           Imlib_init,
           IMLIB_LIBS=$GFX_LIB_IMLIB; AC_DEFINE(HAVE_IMLIB),,
           $GFX_LIB_IMLIB)
	 AC_CHECK_LIB(gdk_imlib,
           gdk_imlib_init,
           GDK_IMLIB_LIBS=$GFX_LIB_GDK_IMLIB; AC_DEFINE(HAVE_GDK_IMLIB),,
           $GFX_LIB_GDK_IMLIB)
         ]
)


AC_SUBST(HAVE_IMLIB)
AC_SUBST(HAVE_GDK_IMLIB)
AC_SUBST(IMLIB_LIBS)
AC_SUBST(GDK_IMLIB_LIBS)
AC_SUBST(IMLIB_CFLAGS)
AC_SUBST(GDK_IMLIB_CFLAGS)

AC_MSG_CHECKING(XFce directory structure)
if test "x$datadir" = "x\${prefix}/share" -o "x$prefix" = "xNONE"; then
  XFCE_DIR="/usr/share/xfce"
else
  XFCE_DIR="$datadir/xfce"
fi
AC_SUBST(XFCE_DIR)
XFCE_LOCALE_DIR="$XFCE_DIR/locale"
AC_SUBST(XFCE_LOCALE_DIR)
AC_MSG_RESULT(XFCE_DIR set to $XFCE_DIR)
AC_MSG_RESULT(XFCE_LOCALE_DIR set to $XFCE_LOCALE_DIR)

dnl enable CDE-specific startup files?
AC_MSG_CHECKING(install CDE specific files)
AC_ARG_ENABLE(dt,
        [  --enable-dt             enable CDE specific files],
        [USE_DT=$enableval],
        [USE_DT=no]
)
AC_MSG_RESULT($USE_DT)
AC_SUBST(USE_DT)

AC_ARG_WITH(dt-suffix,
[  --with-dt-suffix=STR    string to append to CDE filenames],[
  AC_MSG_CHECKING(for string to append to CDE filenames)
  DT_SUFFIX="$withval"
  AC_MSG_RESULT($DT_SUFFIX)
])
AC_SUBST(DT_SUFFIX)

AC_MSG_CHECKING(compiler flags)
dnl Only use -Wall if we have gcc
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi

AC_OUTPUT(Makefile libs/Makefile xfce/Makefile xfwm/Makefile \
	xftree/Makefile xfpager/Makefile xfmouse/Makefile \
	xfbd/Makefile xfclock/Makefile xfsound/Makefile xfgnome/Makefile \
	scripts/Makefile intl/Makefile po/Makefile.in \
	other/Makefile.in backdrops/Makefile.in icons/Makefile.in \
	palettes/Makefile.in sounds/Makefile.in dt/Makefile \
	html/Makefile.in textures/Makefile.in)
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" scripts/xfce_setup.in > scripts/xfce_setup])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" scripts/xfhelp.in > scripts/xfhelp])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" scripts/startxfce.in > scripts/startxfce])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfbdrc.in >  other/sample.xfbdrc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfsoundrc.in > other/sample.xfsoundrc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfce3rc.in >  other/sample.xfce3rc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfce3rc.ja.in >  other/sample.xfce3rc.ja])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfce3rc.ko.in >  other/sample.xfce3rc.ko])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfce3rc.zh_CN.GB2312.in >  other/sample.xfce3rc.zh_CN.GB2312])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfce3rc.zh_TW.Big5.in >  other/sample.xfce3rc.zh_TW.Big5])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfwmrc.in >  other/sample.xfwmrc])
AC_OUTPUT_COMMANDS([sed -e "s%XFCE_DIR%$XFCE_DIR%g" other/sample.xfwmrc.in >  other/system.xfwmrc])
AC_OUTPUT_COMMANDS([sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r other/DISTFILES.in" other/Makefile.in > other/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r backdrops/DISTFILES.in" backdrops/Makefile.in > backdrops/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r textures/DISTFILES.in" textures/Makefile.in > textures/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r icons/DISTFILES.in" icons/Makefile.in > icons/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r palettes/DISTFILES.in" palettes/Makefile.in > palettes/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r sounds/DISTFILES.in" sounds/Makefile.in > sounds/Makefile])
AC_OUTPUT_COMMANDS([sed -e "/DISTFILES =/r html/DISTFILES.in" html/Makefile.in > html/Makefile])
